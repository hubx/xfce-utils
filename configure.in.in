dnl $Id$
dnl
dnl Copyright (c) 2002-2007
dnl         The Xfce development team. All rights reserved.
dnl
dnl Written for Xfce by Olivier Fourdan <fourdan@xfce.org>, Jasper
dnl Huijsmans <jasper@xfce.org>, Benedikt Meurer <benny@xfce.org>
dnl and Xavier MAILLARD <zedek@fxgsproject.org>.
dnl

m4_define([xfce_utils_version], [4.5.90])

AC_INIT([xfce-utils], [xfce_utils_version], [xfce@xfce.org])
AM_INIT_AUTOMAKE([dist-bzip2])

AC_DEFINE_UNQUOTED([RELEASE_LABEL], ["Xfce 4.4"], [Optional release plain name])
AC_SUBST(RELEASE_LABEL)

AM_CONFIG_HEADER([config.h])

AM_MAINTAINER_MODE

dnl Check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LIBTOOL

dnl Check C Compiler Characteristics
AC_C_CONST
AC_C_INLINE

dnl Check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h unistd.h string.h])
AC_CHECK_FUNCS([daemon setsid])

dnl Check for X11 installed
XDT_CHECK_LIBX11_REQUIRE

dnl Check for default browser to use
AC_MSG_CHECKING([what browser to use by default])
AC_ARG_WITH([browser],
AC_HELP_STRING([--with-browser=BROWSER], [What should be the default browser?]),
  [], [with_browser=""])
BROWSER_DEFAULT=$with_browser
AC_SUBST([BROWSER_DEFAULT])
AC_MSG_RESULT([$with_browser])

dnl Check for default terminal to use
AC_MSG_CHECKING([what terminal to use by default])
AC_ARG_WITH([terminal],
AC_HELP_STRING([--with-terminal=TERMINAL], 
	       [What should be the default terminal?]),
	       [], [with_terminal=""])
TERMINAL_DEFAULT=$with_terminal
AC_SUBST([TERMINAL_DEFAULT])
AC_MSG_RESULT([$with_terminal])

dnl enable GDM-specific startup files?
AC_MSG_CHECKING([whether to install GDM specific files])
AC_ARG_ENABLE([gdm],
AC_HELP_STRING([--enable-gdm], [enable the installation of GDM specific files])
AC_HELP_STRING([--disable-gdm], [do not install GDM specific files (default)]),
	[enable_gdm=$enableval], [enable_gdm=no])
AM_CONDITIONAL([USE_GDM], [test "x$enable_gdm" = "xyes"])

dnl Check where to install GDM session files
if test x"$enable_gdm" = x"yes"; then
  GDM_PREFIX=/etc/X11
  AC_MSG_CHECKING(where GDM keeps session files)
  AC_ARG_WITH(gdm-prefix,
  AC_HELP_STRING([--with-gdm-prefix=STR], [where gdm data are installed (default=/etc/X11)]),
  	[], [with_gdm_prefix=/etc/X11])
  GDM_PREFIX=$with_gdm_prefix
  AC_SUBST(GDM_PREFIX)
  AC_MSG_RESULT($with_gdm_prefix)
fi

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for required packages
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.6.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.4.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.4.0])

dnl check for dbus and libdbus-glib
AM_CONDITIONAL([HAVE_DBUS], [test "x$DBUS_FOUND" = "xyes"])
XDT_CHECK_OPTIONAL_PACKAGE([DBUS], [dbus-glib-1], 
                           [0.33], [dbus], [D-BUS support])


dnl Check for debugging support
XDT_FEATURE_DEBUG

dnl Check for vendor specific information
AC_MSG_CHECKING([for additional vendor info])
AC_ARG_WITH([vendor-info],
AC_HELP_STRING([--with-vendor-info=FILE], [Specify an additional file that should be displayed in the about dialog]),
  [], [with_vendor_info=])
if test x"$with_vendor_info" != x""; then
  AC_DEFINE_UNQUOTED([VENDOR_INFO], ["$with_vendor_info"], [Additional vendor info])
  AC_MSG_RESULT([$with_vendor_info])
else
  AC_MSG_RESULT([none])
fi

AC_OUTPUT([
xfce-utils.spec
Makefile
po/Makefile.in
doc/Makefile
doc/index/Makefile
doc/index/C/Makefile
doc/index/fr/Makefile
doc/index/it/Makefile
doc/xfce-utils/Makefile
doc/xfce-utils/C/Makefile
doc/xfce-utils/C/images/Makefile
doc/xfce4-user-guide/Makefile
doc/xfce4-user-guide/C/Makefile
doc/xfce4-user-guide/C/images/Makefile
icons/Makefile
icons/48x48/Makefile
xfrun/Makefile
scripts/Makefile
gdm/Makefile
xfce4-about/Makefile
])

